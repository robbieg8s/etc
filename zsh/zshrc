# vi mode
bindkey -v
# bindings for insert mode
bindkey "^J" history-beginning-search-forward
bindkey "^K" history-beginning-search-backward
bindkey "^X" vi-cmd-mode
# According to my reading of https://en.wikipedia.org/wiki/ANSI_escape_code (under `Terminal input sequences`),
# these are the vt keycodes. I've set the corresponding keystroke directly in the iTerm config to ensure that
# they're right locally
bindkey "\e[3;3~" backward-delete-word # Alt-Delete
bindkey "\e[3;9~" backward-kill-line   # Cmd-Delete
# bindings for command mode
# bindkey -a ...

## Establish file system context
# Prompt expand %x to get current file, absolutize
ZSHRC="${$(print -P '%x'):A}"
# We want the directory often enough also
ETC_ZSH_DIR="${ZSHRC:h}"

## Sundry zsh options
setopt AUTO_PUSHD
setopt GLOB_DOTS
setopt EXTENDED_GLOB
setopt EXTENDED_HISTORY
setopt HIST_EXPIRE_DUPS_FIRST
setopt HIST_FIND_NO_DUPS
setopt INC_APPEND_HISTORY
setopt NO_AUTO_MENU
setopt PUSHD_IGNORE_DUPS
setopt PUSHD_MINUS
setopt RC_QUOTES

## Sundry zsh parameters
DIRSTACKSIZE=48
HISTFILE="${HOME}/.local/share/halfyak/etc/zsh/history"
HISTSIZE=1000
SAVEHIST=1000

## Prompt
# Don't export PS variables, because other shells don't understand the zsh escapes
# Don't forget to
#   setopt PROMPT_SUBST
# if you once again set variables in precmd that are used in the prompt
() {
  # Include a note if running dockerized.
  [[ -e /.dockerenv ]] && local notify_docker=" docker"
  # Probably would be good to extend this to "ssh'd in", running in JetBrains IDE, etc.
  # lsof -p $PPID might be interesting here, esp lsof -a -p $PPID -d cwd -Fn
  # but be careful of lsof in docker because busybox's lsof is not full featured
  # For a use case i have to hand, flag subshells
  PS1="%F{red}%B:%(2L. subshell.)${notify_docker-};%b%f "
  PS2=':%_; '
}

## Configure sqlite shell history
# db location and driver script are in set up in zshenv
# allocate a session per shell invocation
export HALFYAK_ETC_SQLITE_HISTORY_SESSION="$(uuidgen)"
# hook zshaddhistory to save commands, queries have convenience aliases
halfyak_etc_zshaddhistory_sqlite_history_save() {
  # If things aren't set up properly, do a null command. This ensures we don't break regular history in this case.
  # The interactive interface (see aliases file) will fail noisily to get attention.
  "${HALFYAK_ETC_SQLITE_HISTORY:-:}" "${HALFYAK_ETC_SQLITE_HISTORY_DB}" save "${HALFYAK_ETC_SQLITE_HISTORY_SESSION}" "$@"
}
zshaddhistory_functions=($zshaddhistory_functions halfyak_etc_zshaddhistory_sqlite_history_save)

## Auto sourcing infrastructure
() {
  local sourceables="${ETC_ZSH_DIR}"/libexec/sourceables
  [[ -r "${sourceables}" ]] && {
    source "${sourceables}"
    precmd_functions=($precmd_functions halfyak_etc_precmd_sourceables_reload)
  }
}

## Environment/Command specific additional configuration
# half_etc_after is defined in zshenv
# The sequencing here is tricky, since on macOS we need to look up compinit's location to fix
# the fpath so we get the right _git completion, but compinit itself uses the fpath to pick the
# right _git. In general i'd do fpath reorganization earlier (to reduce surprises from anything
# else that uses autoload). So put it right need compinit (to emphasize the coupling), and resolve
# compinit before running the after. We hard (-R) resolve to improve the chance that i find out
# if there are any breakages in this approach.

autoload -R compinit
halfyak_etc_after "${ZSHRC}"

## Autocompletion configuration
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}'
zstyle ':completion:*:*:*:*:messages' format "%d"

autoload -U +X compinit && compinit
# I've experimented with
#   setopt automenu
#   zstyle ':completion:*' menu select
# to get arrow selected completion, but i need to tune/train more to like it

## Interactive aliases
# Remove all other aliases first - this was motivated by misbehaving utilities which add functions
# that shadow executables, but seems reasonable enough to just do in general.
unalias -a
source "${ETC_ZSH_DIR}"/aliases
