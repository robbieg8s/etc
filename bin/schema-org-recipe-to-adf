#!/usr/bin/env jq --compact-output --from-file

def collectMethodTexts:
  [ .recipeInstructions[] | (
    if type == "string" then .
    elif .["@type"] == "HowToStep" then .text
    elif .["@type"] == "HowToSection" then (
      .name,
      .itemListElement[].text
    )
    end
  ) ]
  ;

{
  type: "doc",
  version: 1,
  content: [
    (
      { type: "text", text: .description },
      { type: "heading", attrs: { level: 2 }, content: [ { type: "text", text: "Ingredients" } ] },
      { type: "bulletList",
        content: .recipeIngredient | map ( {
          type: "listItem",
          content: [ {
            type: "paragraph",
            content: [ { type: "text", text: .} ]
          } ]
        } )
      },
      { type: "heading", attrs: { level: 2 }, content: [ { type: "text", text: "Method" } ] },
      { type: "orderedList",
        content: collectMethodTexts | map ( {
          type: "listItem",
          content: [ {
            type: "paragraph",
            content: [ { type: "text", text: .} ]
          } ]
        } )
      }
    ),
    { type: "heading", attrs: { level: 2 }, content: [ { type: "text", text: "Links" } ] },
    ( (.url // .mainEntityOfPage // empty) | { type: "paragraph",
          content: [ {
            type: "inlineCard",
            attrs: { url: . }
          }, {
            type: "text",
            text: " "
          } ]
    } )
  ]
}
