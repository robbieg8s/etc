#!/bin/zsh

set -ex

retry() {
  local limit="${1:?}"
  shift
  local tries=0
  until "$@"
  do
    : $((++tries))
    if [[ "${tries}" -ge "${limit}" ]]
    then
      printf "Exiting after %s tries\n" "${tries}"
      false
    fi
  done
}

# Save Brew config
BREWFILE="${HOME}/cloud/etc/homebrew/Brewfile"
mkdir -p "${BREWFILE%/*}"
touch "${BREWFILE}"
cp "${BREWFILE}" "${BREWFILE}.bak"
rm "${BREWFILE}"
brew bundle dump --file="${BREWFILE}"
# The bundle dump does not include information about what was linked, so record
# this information also. It's not in brew info --json=v2 itself, so peek into
# Homebrew's system of record in the file system. Since we're doing that anyway,
# use the Cellar not `brew list` or `brew info`, since that's much faster. I'm
# not sure what format i want this in yet - so names will do for now
UNLINKED="${HOME}/cloud/etc/homebrew/unlinked.txt"
join -v1 =(ls /opt/homebrew/Cellar) =(ls /opt/homebrew/var/homebrew/linked) > "${UNLINKED}"

# Update and Upgrade Brew
brew update
# Don't auto upgrade casks
# - openjdk8, needed by plantuml, is interactive
# - you can't pin casks to avoid auto upgrade
retry 3 brew upgrade --formula
# Notify if casks are out of date
() {
  output="${1:?}"
  brew upgrade --dry-run --cask > "${output}"
  if [[ -s "${output}" ]]
  then
    upgrades="$(sed -ne '/^.*upgrade \(.* outdated packages\{0,1\}\).*$/s//\1/p' <"${output}")"
    sendmail -i "${USER}"<<END_MAIL
Subject: Brew cask: ${upgrades:-output parser broken}

$(cat ${output})

:; brew upgrade --cask
END_MAIL
  fi
} =(true)
# Notify if we have installed formulae that were installed as dependencies and
# are not dependencies of another installed formula or cask
() {
  output="${1:?}"
  brew leaves --installed-as-dependency > "${output}"
  if [[ -s "${output}" ]]
  then
    leaf_count="$(awk 'END {print NR}' "${output}")"
    sendmail -i "${USER}"<<END_MAIL
Subject: Brew detritus - at least ${leaf_count} stale formulae

$(cat ${output})

:; brew leaves --installed-as-dependency | xargs brew uninstall

You may need to iterate the above.

You should also investigate

:; brew autoremove

as it might do all the work. Update this message if it works.
END_MAIL
  fi
} =(true)
