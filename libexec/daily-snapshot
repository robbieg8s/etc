#!/bin/zsh -ef

log() {
  local fmt="${1:?}"
  shift
  print -f "$fmt\n" -- "$@"
}

fail() {
  log "$@"
  exit 1
}

SERIAL="$(system_profiler -json SPHardwareDataType | jq --raw-output '.SPHardwareDataType[0].serial_number')"
SNAPSHOT_ROOT="${HOME}/Documents/etc/local/${SERIAL}"

if [[ ! -d "${SNAPSHOT_ROOT}" ]]
then
  fail 'No snapshot root at %s, cannot snapshot' "${(q+)SNAPSHOT_ROOT}"
else
  for dir in ~/src/*(/)
  do
    local name="${dir##*/}"
    local snapshot="${SNAPSHOT_ROOT}/src/${name}"
    mkdir -p "${snapshot}"
    git -C "${dir}" config get remote.origin.url > "${snapshot}/remote.origin.url"
  done

  asdf list > "${SNAPSHOT_ROOT}/asdf"

  mkdir -p "${SNAPSHOT_ROOT}/alfredapp"
  rsync --checksum --delete --links --recursive \
    ~/"Library/Application Support/Alfred/Alfred.alfredpreferences"/ \
    "${SNAPSHOT_ROOT}/alfredapp/Alfred.alfredpreferences"/

  mkdir -p "${SNAPSHOT_ROOT}/iterm"
  rsync --checksum --delete --links --recursive \
    ~/"Library/Preferences/com.googlecode.iterm2.plist" \
    "${SNAPSHOT_ROOT}/iterm/com.googlecode.iterm2.plist"
fi
